import os
Import('env')

#
# Need to make a copy because SCons uses the environment.
# at it's final state, so StGermain ends up depending on
# StgDomain, etc. 
#

env = env.Clone()
env['CURR_PROJECT'] = "StGermain"

#
#  Build the 'pcu' sub-project.
#

SConscript('pcu/SConscript', exports='env')
env.Prepend(LIBS=['pcu'])

#
# Inside each project we will be accessing headers without the
# project name as a build_dir, so we need to let SCons know how to
# find those headers.
#

env.Append(CPPPATH=env['build_dir'] + '/include/StGermain')

#
# Keep a list of all the objects we build so we can make a library
# afterwards.
#

objs = []
suites = []
tst_exp = []
tst_input = []

#
# Process each directory uniformly.
#

dirs = Split('Base/Foundation Base/IO Base/Container Base/Automation Base/Extensibility ' \
             'Base/Context Base Utils libStGermain')
for d in dirs:
    # Need the module name, which is just the directory.
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', []) 

    # Setup where to look for files.
    src_dir = d + '/src'
    inc_dir = 'include/StGermain/' + d
    tst_dir = d + '/tests'
    tst_exp_dir = tst_dir + '/expected'
    tst_input_dir = tst_dir + '/input'
    tst_install_dir = 'tests/StGermain/' + d

    # Install the headers
    hdrs = env.Install(inc_dir, Glob(src_dir + '/*.h'))

    # Build our source files.
    srcs = Glob(src_dir + '/*.c') +  Glob(src_dir + '/*.cpp')
    for src in srcs:
        stg_cpp_defs = cpp_defs + [('SOURCE_FILE', '"\\"' + src.name + '\\""')]
        objs += env.SharedObject(src, CPPDEFINES=stg_cpp_defs )

    # Build any test suites we might find.
    suites += env.Object(Glob(tst_dir + '/*Suite.c'))

    # Install any test expected and input files
    tst_exp += env.Install(tst_install_dir + '/expected', Glob(tst_exp_dir + '/*'))
    tst_input += env.Install(tst_install_dir + '/input', Glob(tst_input_dir + '/*'))

# Need to install headers from libStGermain.
hdrs = env.Install('include/StGermain', Glob('libStGermain/src/*.h'))

#
# Build shared library.
#

if env['shared_libs']:
    env.SharedLibrary('lib/StGermain', objs)
    env.SharedLibrary(env['prefix'] + '/lib/StGermain', objs)

#
# Build static library.
#

if env['static_libs']:

    # In order to allow other projects to register their modules,
    # we copy a template file to a standard location and direct
    # SCons to build it in to the final static library. Before the
    # build actually takes place the other projects will fill in
    # there details.
    dir = Dir(env['build_dir'] + '/StGermain').abspath
    if not os.path.exists(dir):
        os.makedirs(dir)
    f = File(env['build_dir'] + '/StGermain/stg_static_modules.c')
    Execute(Copy(f.abspath, File('libStGermain/src/stg_static_modules.c.tmpl')))

    # Now build the library.
    l = env.StaticLibrary(env['build_dir'] + '/lib/StGermain', objs)
    env.Install(env['prefix'] + '/lib', l)

# test runner program

libs = ['StGermain'] + env.get('LIBS', [])
env.PCUTest('tests/testStGermain', suites,
    PCU_LIBHEADERS="#include <StGermain/StGermain.h>",
    PCU_SETUP="StGermain_Init(&argc, &argv);\n" \
    "   stJournal->firewallProducesAssert = True;\n" \
    "   stJournal->firewallProtected = True;",
    PCU_TEARDOWN="StGermain_Finalise();",
    LIBS=libs,
    PCU_EXP=tst_exp,
    PCU_INPUT=tst_input,
    PROJECT="StGermain")
