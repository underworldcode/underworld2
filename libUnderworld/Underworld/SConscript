import os
Import('env')

#  
# Need to make a copy because SCons uses the environment
# at it's final state, so StGermain ends up depending on
# StgDomain, etc..
#

env = env.Clone()
env['CURR_PROJECT'] = "Underworld"

#
# Inside each project we will be accessing headers without the
# project name as a prefix, so we need to let SCons know how to
# find those headers.
#

env.Append(CPPPATH=env['build_dir'] + '/include/Underworld')

#
# Need to include the Underworld library for binaries/plugins.
#

libs = ['Underworld'] + env.get('LIBS', [])

# Keep a list of all the objects we build so we can make a library
# afterwards.
objs = []
suites = []
tst_exp = []
tst_input = []

#
# Process each directory uniformly.
#

dirs = Split('Function Rheology Utils libUnderworld')
for d in dirs:
    # Need the module name, which is just the directory.
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    version_name = 'UNDERWORLD_VERSION'
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', [])

    # Setup where to look for files.
    src_dir = d + '/src'
    inc_dir = 'include/Underworld/' + d
    tst_dir = d + '/tests'
    tst_exp_dir = tst_dir + '/expected'
    tst_input_dir = tst_dir + '/input'
    tst_install_dir = 'tests/Underworld/' + d

    # Install the headers and '.def' files.
    env.Install(inc_dir, Glob(src_dir + '/*.h'))
    env.Install(inc_dir, Glob(src_dir + '/*.hpp'))

    # Build our source files.
    srcs = Glob(src_dir + '/*.c') +  Glob(src_dir + '/*.cpp')
    for src in srcs:
        stg_cpp_defs = cpp_defs + [('SOURCE_FILE', '"\\"' + src.name + '\\""')]
        objs += env.SharedObject(src, CPPDEFINES=stg_cpp_defs)

    # Build any test suites we might find.
    suites += env.Object(Glob(tst_dir + '/*Suite.c'))

    # Install any test expected and input files
    tst_exp += env.Install(tst_install_dir + '/expected', Glob(tst_exp_dir + '/*'))
    tst_input += env.Install(tst_install_dir + '/input', Glob(tst_input_dir + '/*'))

dirs = ['SysTest/AnalyticPlugins/Velic_solCx',
             'SysTest/AnalyticPlugins/Velic_solKx',
             'SysTest/AnalyticPlugins/Velic_solKz']
for d in dirs:
    # Need tee module name, which is just the directory.
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    version_name = 'UNDERWORLD_VERSION'
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', [])

    # Setup where to look for files.
    src_dir = d
    inc_dir = 'include/Underworld/' + d

    # Install the headers and '.def' files.
    env.Install(inc_dir, Glob(src_dir + '/*.h'))

    # Build our source files.
    srcs = Glob(src_dir + '/*.c') +  Glob(src_dir + '/*.cpp')
    for src in srcs:
        stg_cpp_defs = cpp_defs + [('SOURCE_FILE', '"\\"' + src.name + '\\""')]
        objs += env.SharedObject(src, CPPDEFINES=stg_cpp_defs)


# Need to install headers from libUnderworld.
env.Install('include/Underworld', Glob('libUnderworld/src/*.h'))

#
# Build plugins.
#
# Added an Import/export command so that items added here are added to env and can be used by other 
# scons executables. Added it as Dictionaries so that the Toolbox ID can be preserved.
# --KathleenHumble -20-08-2010

Import('DOXYGEN_DIRS')
pluginDirs  = ['libUnderworld/Toolbox',
#        'SysTest/AnalyticPlugins/VelicIC',
#        'SysTest/AnalyticPlugins/DepthDependentAnalytic3D',
#	    'SysTest/AnalyticPlugins/Trubitsyn2006',
        'SysTest/AnalyticPlugins/Velic_solA',
        'SysTest/AnalyticPlugins/Velic_solB',
#        'SysTest/AnalyticPlugins/Velic_solCx',
#        'SysTest/AnalyticPlugins/Velic_solKx',
        'SysTest/AnalyticPlugins/Velic_solHA',
#        'SysTest/AnalyticPlugins/Velic_solKz',
        'SysTest/AnalyticPlugins/Velic_solS',
#        'SysTest/AnalyticPlugins/PlaneWaveShearAdvectionDiffusion'
         ]


DOXYGEN_DIRS['Underworld'] = pluginDirs
Export('DOXYGEN_DIRS')
pl_objs = []
pl_regs = []
for d in pluginDirs:

    name = 'Underworld_' + d.split('/')[-1] + 'module'
    mod_name = env['ESCAPE']('"' + ''.join(d.split('/')) + '"')
    cpp_defs = [('CURR_MODULE_NAME', mod_name)] + env.get('CPPDEFINES', []) 
    
    env.Install('include/Underworld/' + d.split('/')[-1], Glob(d + '/*.h'))

    srcs = Glob(d + '/*.c')
    cur_objs = env.SharedObject(srcs, CPPDEFINES=cpp_defs)

    # If we have shared libraries, build the dynamic plugin.
    if env['shared_libs']:
        lib_pre = env['LIBPREFIXES']
        if not isinstance(lib_pre, list):
            lib_pre = [lib_pre]
        env.SharedLibrary('lib/' + name, cur_objs,
                          SHLIBPREFIX='',
                          LIBPREFIXES=lib_pre + [''],
                          LIBS=libs)

    # If we are building static libs we need to construct a C file
    # mapping the plugin's name to its register function.
    if env['static_libs']:
        pl_regs += [name]

    # Keep track of all the plugin objects.
    pl_objs += cur_objs

Import('DOXYGEN_PLUGINS')
DOXYGEN_PLUGINS['Underworld'] = pl_objs
Export('DOXYGEN_PLUGINS')
#
# Build shared library.
#

if env['shared_libs']:
    env.SharedLibrary('lib/Underworld', objs)
    env.SharedLibrary(env['prefix'] + '/lib/Underworld', objs)

#
# Write the static registry code.
#

if env['static_libs']:

    reg_c = '#include <StGermain/StGermain.h>\n\n'
    reg_c += 'extern int stg_num_modules;\n'
    reg_c += 'extern char **stg_module_names;\n'
    reg_c += 'extern int *stg_num_module_syms;\n'
    reg_c += 'extern char ***stg_module_syms;\n\n'
    reg_c += 'extern void ***stg_module_funcs;\n\n'
    for n in pl_regs:
        n = n[:-6]
        reg_c += 'extern void (%s_Register)();\n'%n
        if n.find('Toolbox') != -1:
            reg_c += 'extern void (%s_Initialise)();\n'%n
            reg_c += 'extern void (%s_Finalise)();\n'%n
            reg_c += 'extern void (%s_GetDeps)();\n'%n
    reg_c += '\n'

    reg_c += 'void underworld_register_static_modules() {\n'
    reg_c += '   int new_num = stg_num_modules + %d;\n\n'%len(pl_regs)
    
    reg_c += '   stg_module_names = (char**)realloc(stg_module_names, new_num*sizeof(char*));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        reg_c += '   stg_module_names[stg_num_modules + %d] = (char*)"%s";\n'%(i, n)
        
    reg_c += '\n   stg_num_module_syms = (int*)realloc(stg_num_module_syms, new_num*sizeof(int));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 4
        else:
            num_syms = 1
        reg_c += '   stg_num_module_syms[stg_num_modules + %d] = %d;\n'%(i, num_syms)

    reg_c += '\n   stg_module_syms = (char***)realloc(stg_module_syms, new_num*sizeof(char**));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 4
        else:
            num_syms = 1
        reg_c += '   stg_module_syms[stg_num_modules + %d] = (char**)malloc(%d*sizeof(char*));\n'%(i, num_syms)
        reg_c += '   stg_module_syms[stg_num_modules + %d][0] = (char*)"%s_Register";\n'%(i, n)
        if n.find('Toolbox') != -1:
            reg_c += '   stg_module_syms[stg_num_modules + %d][1] = (char*)"%s_Initialise";\n'%(i, n)
            reg_c += '   stg_module_syms[stg_num_modules + %d][2] = (char*)"%s_Finalise";\n'%(i, n)
            reg_c += '   stg_module_syms[stg_num_modules + %d][3] = (char*)"%s_GetDeps";\n'%(i, n)

    reg_c += '\n   stg_module_funcs = (void***)realloc(stg_module_funcs, new_num*sizeof(void**));\n'
    for i in range(len(pl_regs)):
        n = pl_regs[i][:-6]
        if n.find('Toolbox') != -1:
            num_syms = 4
        else:
            num_syms = 1
        reg_c += '   stg_module_funcs[stg_num_modules + %d] = (void**)malloc(%d*sizeof(void*));\n'%(i, num_syms)
        reg_c += '   stg_module_funcs[stg_num_modules + %d][0] = (void*)%s_Register;\n'%(i, n)
        if n.find('Toolbox') != -1:
            reg_c += '   stg_module_funcs[stg_num_modules + %d][1] = (void*)%s_Initialise;\n'%(i, n)
            reg_c += '   stg_module_funcs[stg_num_modules + %d][2] = (void*)%s_Finalise;\n'%(i, n)
            reg_c += '   stg_module_funcs[stg_num_modules + %d][3] = (void*)%s_GetDeps;\n'%(i, n)

    reg_c += '\n   stg_num_modules += %d;\n'%len(pl_regs)
    reg_c += '}\n'

    reg_filename = os.path.join(env['build_dir'], 'Underworld', 'underworld_static_modules.c')
    if not os.path.exists(os.path.dirname(reg_filename)):
        os.makedirs(os.path.dirname(reg_filename))
    reg_file = open(reg_filename, 'w')
    reg_file.write(reg_c)
    reg_file.close()
    reg_obj = env.Object(reg_filename)

    # Add our register function to the StGermain module file.
    f = open(File(env['build_dir'] + '/StGermain/stg_static_modules.c').abspath, 'r')
    txt = f.readlines()
    f.close()
    txt.insert(-2, '   underworld_register_static_modules();\n')
    txt.insert(0, 'void underworld_register_static_modules();\n')
    f = open(File(env['build_dir'] + '/StGermain/stg_static_modules.c').abspath, 'w')
    f.writelines(txt)
    f.close()

    # Static library.
    l = env.StaticLibrary(env['build_dir'] + '/lib/Underworld', objs + pl_objs + reg_obj)
    env.Install(env['prefix'] + '/lib', l)

#
# create Underworld executable -> it's just a copy of StGermain executable
#

env.InstallAs(env['build_dir']+ '/bin/Underworld', env['build_dir']+ '/bin/StGermain')
env.Alias("install", env.InstallAs(os.path.join(env['prefix'],"bin")+'/Underworld', env['build_dir']+ '/bin/StGermain')) 

#
# Test runner program.
#

env.PCUTest('tests/testUnderworld', suites,
    PCU_LIBHEADERS="#include <StGermain/StGermain.h>\n" \
        "#include <StgDomain/StgDomain.h>\n" \
        "#include <StgFEM/StgFEM.h>\n" \
        "#include <PICellerator/PICellerator.h>\n" \
        "#include <Underworld/Underworld.h>", 
    PCU_SETUP="\n" \
        "#ifdef NOSHARED\n" \
        "   stgdomain_register_static_modules();\n" \
        "   stgfem_register_static_modules();\n" \
        "   picellerator_register_static_modules();\n" \
        "   underworld_register_static_modules();\n" \
        "   StGermain_Init(&argc, &argv);\n" \
        "   StgDomain_Init(&argc, &argv);\n" \
        "   StgFEM_Init(&argc, &argv);\n" \
        "   PICellerator_Init(&argc, &argv);\n" \
        "   Underworld_Init(&argc, &argv);\n" \
        "#else\n" \
        "   StGermain_Init(&argc, &argv);\n" \
        "   ModulesManager_LoadModule(stgToolboxesManager, \"Underworld\" );\n" \
        "#endif\n\n" \
        "   stJournal->firewallProducesAssert = True;\n" \
        "   stJournal->firewallProtected = True;", 
    PCU_TEARDOWN="\n" \
        "#ifdef NOSHARED\n" \
        "   Underworld_Finalise();\n" \
        "   PICellerator_Finalise();\n" \
        "   StgFEM_Finalise();\n" \
        "   StgDomain_Finalise();\n" \
        "   StGermain_Finalise();\n" \
        "#else\n" \
        "   ModulesManager_UnloadModule(stgToolboxesManager, \"Underworld\" );\n" \
        "   StGermain_Finalise();\n" \
        "#endif\n",
    LIBS=libs,
    PCU_EXP=tst_exp,
    PCU_INPUT=tst_input,
    PROJECT="Underworld")

#
# Install XML input files.
#

env.Install('lib/StGermain/Underworld', Glob('InputFiles/Underworld_Components/*.xml'))
env.Install('lib/StGermain/Underworld/BaseApps', Glob('InputFiles/BaseApps/*.xml'))
env.Install('lib/StGermain/Underworld/VariableConditions', Glob('InputFiles/VariableConditions/*.xml'))
env.Install('lib/StGermain/Underworld/Viewports', Glob('InputFiles/Viewports/*.xml'))
env.Install('lib/StGermain/Underworld/AnalyticFields', Glob('InputFiles/AnalyticFields/*.xml'))
env.Alias("install", env.Install(env['prefix'] + '/share/Underworld/', env.GetLaunchDir() + '/Underworld/InputFiles' ) ) 

#
# Setup the system tests.
#

